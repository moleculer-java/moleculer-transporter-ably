apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	options.encoding = 'UTF-8'
	failOnError = false
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    } 
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

// --- SOURCE ---

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
	}
	test {
	    resources {
	        srcDirs = ['src/test/java']
	    }
	}
}

// --- DEPENDENCIES ---

dependencies {
	
    // ============== COMPILATION ==============
    
    testImplementation 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'
	
    // =============== MOLECULER ===============

	// Moleculer core (required)
	implementation group: 'com.github.berkesa', name: 'moleculer-java', version: '1.2.5'
	    
    // ================ LOGGING ================
	
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
       
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.28'

	// https://mvnrepository.com/artifact/org.slf4j/log4j-over-slf4j
	implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.28'

	// https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
	implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.28'

    // =========== ABLY.IO PUB/SUB =============
	
	// https://mvnrepository.com/artifact/io.ably/ably-java
	implementation group: 'io.ably', name: 'ably-java', version: '1.1.8'
	
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

// version = '1.0.0-SNAPSHOT'
version = '1.0.0'

modifyPom {
    project {
        artifactId 'moleculer-transporter-ably'
        name 'Ably.io Realtime Transporter'
        description 'Ably.io Realtime Transporter for Moleculer Java.'
        url 'https://moleculer-java.github.io/moleculer-transporter-ably/'
        inceptionYear '2019'

        scm {
            url 'https://moleculer-java.github.io/moleculer-transporter-ably/'
            connection 'scm:https://github.com/moleculer-java/moleculer-transporter-ably.git'
            developerConnection 'scm:git://github.com/moleculer-java/moleculer-transporter-ably.git'
        }

        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'moleculer-java'
                version '1.2.5'
                scope 'runtime'
            }
            dependency {
                groupId 'io.ably'
                artifactId 'ably-java'
                version '1.1.8'
                scope 'runtime'
            }
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}